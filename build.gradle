buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
    }
}

apply plugin: 'groovy'
apply plugin: 'com.jfrog.bintray'

version = currentVersion
group = 'ru.d10xa'

sourceCompatibility = 1.5

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    testCompile 'junit:junit:4.12'
}

configurations {
    published
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    published sourceJar
}

bintray {
    user = this.hasProperty('bintrayUser') ? bintrayUser : ''
    key = this.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
    configurations = ['published', 'archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'ru.d10xa:gradle-download-xml-plugin'
        desc = 'A Gradle plugin for downloading xml files(wsdl,xsd) with recursive imports and includes'
        websiteUrl = 'https://github.com/d10xa/gradle-download-xml-plugin'
        issueTrackerUrl = 'https://github.com/d10xa/gradle-download-xml-plugin/issues'
        vcsUrl = 'https://github.com/d10xa/gradle-download-xml-plugin.git'
        licenses = ['MIT']
        labels = ['xml','wsdl','xsd','download', 'gradle', 'plugin']
        publicDownloadNumbers = true
        version {
            name = project.version
            attributes = [ 'gradle-plugin': 'ru.d10xa.download-xml:ru.d10xa:gradle-download-xml-plugin' ]
        }
    }
}